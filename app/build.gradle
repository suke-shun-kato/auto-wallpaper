//// apply(plugin: 'com.android.application') の省略記法、名前付き引数
apply plugin: 'com.android.application'


//// Androidアプリのビルドのための設定をここに記述する
android {
    compileSdkVersion 28
    // 設定、ここの記述はマニュフェストに埋め込まれる
    defaultConfig {
        applicationId "xyz.goodistory.autowallpaper"
        minSdkVersion 19    //Android 4.4
        targetSdkVersion 28 // Android 9.0 Pie
        versionCode 18
        versionName "0.1.17" //ここはversionCodeとは1ずれる
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // ビルドタイプの設定（リリース用のビルド、デバッグ用のなど別々に設定できる）
    buildTypes {
        debug { //デバッグ用のビルドタイプの設定
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "debug"
        }
        release {   //リリース用のビルドタイプの設定
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //
    testOptions {
        unitTests.returnDefaultValues = true
    }


    lintOptions {
        checkReleaseBuilds false
    }
}

//// 使用ライブラリの記述（依存関係の記述）
//// （注意）Android Plugin for Gradle 3.0.0（Gradle3.0ではない）の書き方は2.0から結構変更されているので注意
//// 参考資料 https://developer.android.com/studio/build/gradle-plugin-3-0-0-migration.html
dependencies {
    // ---------------------------------------------------------------------------
    // 通常コードの依存関係の記述
    // 2.0では「compile」で記述されていたもの
    // ---------------------------------------------------------------------------
    // -------------------------------------
    // デフォルトの設定
    // -------------------------------------
    // ディレクトリ階層のlibsフォルダの中の.jarファイルを読み込む
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // com.android.support の appcompat-v7 の 26.1.0 のライブラリを読み込む
    // （リポジトリの設定はルートの方のbuild.gradleのallprojectsで設定されている）
    implementation 'androidx.appcompat:appcompat:1.0.2'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // -------------------------------------
    // 広告（add）
    // -------------------------------------
    implementation 'com.google.android.gms:play-services-ads:18.1.0'


    // -------------------------------------
    // OAuth1.0&2.0ライブラリ
    // -------------------------------------
    // バージョン5.0.0はAPIレベル26（Android 8.0 Oreo）以上でしか対応していないので敢えて4.2.0使用
    // noinspection GradleDependency
    implementation 'com.github.scribejava:scribejava-apis:4.2.0'

    // -------------------------------------
    // 画像読み込みライブラリ
    // -------------------------------------
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    // ---------------------------------------------------------------------------
    // テスト用のコード（JUnitなどのテスト）での依存関係の記述
    // ---------------------------------------------------------------------------
    // junit の junit の 4.12 のライブラリを読み込む、JUnitは5が出ているがAndroidでは現在使えない
    testImplementation 'junit:junit:4.12'

    // Androidのクラス（ライブラリ）をモックとして読み込めるように設定を記述
    testImplementation 'org.mockito:mockito-core:1.10.19'


    // ---------------------------------------------------------------------------
    // AndroidTest用のコードでの依存関係の記述
    // ---------------------------------------------------------------------------
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
